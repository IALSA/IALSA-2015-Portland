TITLE: Bivariate Latent Growth Model for Grip Strenght and Visuospatial ability_Octo-Twin;!Title to help you differentiate one input/output from another

DATA:  FILE IS spss_octomult.dat; !Name of data file and where data is located 

VARIABLE: Names are Case PairID TwinID Zygosity !All variables in data set kept in same order
    	Female Educyrs EducCat SESgrp Resgrp Smoke 
   	    SESChild SESChOth HlthLif CogPrev LonelyE 
    	Dead DeadAge YTDead TotDem DemEver DemType DemAge YTDem 
    	time1 time2 time3 time4 time5 dedtime1 dedtime2 
    	dedtime3 dedtime4 dedtime5 tvdead1 tvdead2 tvdead3 
    	tvdead4 tvdead5 demtime1 demtime2 demtime3 demtime4 
    	demtime5 tvdem1 tvdem2 tvdem3 tvdem4 tvdem5 
    	CompAge1 CompAge2 CompAge3 CompAge4 CompAge5 
    	Marital1 Marital2 Marital3 Marital4 Marital5 weight1 
    	weight2 weight3 weight4 weight5 height1 height2 
    	height3 height4 height5 bmi1 bmi2 bmi3 bmi4 bmi5 
    	SRhlth1 SRhlth2 SRhlth3 SRhlth4 SRhlth5 living1 
    	living2 living3 living4 living5 SR2hlth1 SR2hlth2 
   	    SR2hlth3 SR2hlth4 SR2hlth5 SROhlth1 SROhlth2 
    	SROhlth3 SROhlth4 SROhlth5 HlthPrv1 HlthPrv2 HlthPrv3 
    	HlthPrv4 HlthPrv5 HlthTra1 HlthTra2 HlthTra3 
    	HlthTra4 HlthTra5 CogPres1 CogPres2 CogPres3 CogPres4 
    	CogPres5 AGames1 AGames2 AGames3 AGames4 AGames5 
    	ACross1 ACross2 ACross3 ACross4 ACross5 ALiter1 
    	ALiter2 ALiter3 ALiter4 ALiter5 AWrite1 AWrite2 
    	AWrite3 AWrite4 AWrite5 AStudy1 AStudy2 AStudy3 
    	AStudy4 AStudy5 AOther1 AOther2 AOther3 AOther4 
    	AOther5 LonelyP1 LonelyP2 LonelyP3 LonelyP4 
    	LonelyP5 FrTalk1 FrTalk2 FrTalk3 FrTalk4 FrTalk5 
    	FrPart1 FrPart2 FrPart3 FrPart4 FrPart5 LackCo1 
    	LackCo2 LackCo3 LackCo4 LackCo5 Abandon1 Abandon2
     	Abandon3 Abandon4 Abandon5 NumFr1 NumFr2 NumFr3 
    	NumFr4 NumFr5 NumPref1 NumPref2 NumPref3 NumPref4
    	NumPref5 SeeChGr1 SeeChGr2 SeeChGr3 SeeChGr4 
    	SeeChGr5 pek1 pek2 pek3 pek4 pek5 gripp1 gripp2 
    	gripp3 gripp4 gripp5 chr5tim1 chr5tim2 chr5tim3 
    	chr5tim4 chr5tim5 padl1 padl2 padl3 padl4 padl5 
    	iadl1 iadl2 iadl3 iadl4 iadl5 digspf1 digspf2 
    	digspf3 digspf4 digspf5 digspb1 digspb2 digspb3 
    	digspb4 digspb5 prose1 prose2 prose3 prose4 prose5 
    	block1 block2 block3 block4 block5 info1 info2
     	info3 info4 info5 synnum1 synnum2 synnum3 synnum4 
    	synnum5 figure1 figure2 figure3 figure4 figure5 
    	digsym1 digsym2 digsym3 digsym4 digsym5 psif1 psif2 
    	psif3 psif4 psif5 mirnam1 mirnam2 mirnam3 mirnam4
     	mirnam5 mirrcl1 mirrcl2 mirrcl3 mirrcl4 mirrcl5 
    	mirrcg1 mirrcg2 mirrcg3 mirrcg4 mirrcg5 mircor1 
    	mircor2 mircor3 mircor4 mircor5 clock1 clock2 
    	clock3 clock4 clock5 mmse1 mmse2 mmse3 mmse4 
    	mmse5 mismmse1 mismmse2 mismmse3 mismmse4 mismmse5 
    	filtmmse1 filtmmse2 filtmmse3 filtmmse4 filtmmse5 
    	highBP1 highBP2 highBP3 highBP4 highBP5 sbp1 sbp2 
    	sbp3 sbp4 sbp5 dbp1 dbp2 dbp3 dbp4 dbp5 diab1 
    	diab2 diab3 diab4 diab5 ExamDate BirthDate DeadDate; 
    
MISSING is all(-9999); !Value used for missing cases 
      
USEVAR are time1-time5 block1-block5 gripp1-gripp5 Female 
         married widowed divorce highses medses 
         edu weight height bmi age80 agesq DAge; !Variables used in analysis. New variables in DEFINE must be included
 
CLUSTER = PairID; !Variables with clustering information (e.g. twins)
       
SUBPOPULATION are DemEver EQ 0;!To select observations (use USEOBSERVATIONS when no cluster is used)

TSCORES= time1-time5;!For individually varying times of observation 

Define: age80 = CompAge1 - 83.282; !To define new variables e.g. to centre variables
        DAge = DeadAge - 90.420; 
        agesq = age80*age80;       
        edu = Educyrs - 7.245;  
        weight = weight1 - 64.455;  
        height = height1 - 162.155; 
        bmi = bmi1 - 24.448;
        if (SESgrp eq 1) then highses = 1;
        if (SESgrp eq 2 or SESgrp eq 3) then highses = 0; 
        if (SESgrp eq 2) then medses = 1;
        if (SESgrp eq 1 or SESgrp eq 3) then medses = 0;  
        if (SESgrp eq 3) then lowses = 1;
        if (SESgrp eq 1 or SESgrp eq 2) then lowses = 0;
        
        if (Marital1 eq 1) then married = 1; 
        if (Marital1 eq 2 or Marital1 eq 3 or Marital1 eq 4)then married = 0; 
        if (Marital1 eq 2) then unmar = 1; 
        if (Marital1 eq 1 or Marital1 eq 3 or Marital1 eq 4)then unmar = 0;  
        if (Marital1 eq 3) then widowed = 1; 
        if (Marital1 eq 1 or Marital1 eq 2 or Marital1 eq 4)then widowed = 0; 
        if (Marital1 eq 4) then divorce = 1; 
        if (Marital1 eq 1 or Marital1 eq 2 or Marital1 eq 3)then divorce = 0;  

ANALYSIS: ESTIMATOR=MLR; TYPE = RANDOM COMPLEX;!Type of analysis requested 

MODEL:  i s| block1-block5 AT time1-time5;!Univariate growth model
        i2 s2| gripp1-gripp5 AT time1-time5;!Univariate growth model
        i s i2 s2;!Variances
        i s i2 s2 WITH i s i2 s2; !Covariances between latent variables. Default in Mplus 
    	i i2 ON age80 agesq edu weight height married widowed
        divorce highses medses bmi DAge Female; !Control for covariates on intercept and slope
        s s2 ON age80 agesq edu weight height married widowed 
        divorce highses medses bmi DAge Female ; !Control for covariates on intercept and slope
          	
        block1-block5 pwith gripp1-gripp5(WPdigsym); !Paired covariances constrained to be equal across time
        block1-block5(1); !Residual Variances constrained to be equal across time          
     	gripp1-gripp5(2); !Residual Variances constrained to be equal across time     

OUTPUT: sampstat; !request sample statistics