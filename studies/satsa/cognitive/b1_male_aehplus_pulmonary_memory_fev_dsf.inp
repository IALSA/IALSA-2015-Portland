TITLE: fev - DSF - male - age & education & height & diab & smoke & cardio

DATA:  File = satsa.dat;

VARIABLE: Names are 
twinnr pairid sex01 przygup rear iage1-iage7 intakeage time1-time7 dem1-dem7 
s_demen_onset
htcm wtkg qualc depr cv chd sumill eversmoke diabete
marital msgroup sesobj educ edugr
fev1_1-fev1_7 ht_1-ht_7 grip1-grip7 gait2-gait7
stupt_2-stupt_7 stuprt_2-stuprt_7
idigf_r1-idigf_r7 idigb_r1-idigb_r7 ibloc_r1-ibloc_r7 ithur_r1-ithur_r7
isynm_r1-isynm_r7 iinfo_r1-iinfo_r7 ianal_r1-ianal_r7 irota_r1-irota_r7
ifigi_r1-ifigi_r7 isymd_r1-isymd_r7 mmse7_1-mmse7_7 
tverb1-tverb7 tspat1-tspat7 tmemo1-tmemo7 tsped1-tsped7 tpcom1-tpcom7;

       MISSING are .;
       USEVAR are  diabete eversmoke chd time1 time2 time3  time5 time6 time7
                   p1 p2 p3  p5 p6 p7
                   c1 c2 c3  c5 c6 c7
                   Bage ceduc height;
       TSCORES =   time1 time2 time3   time5 time6 time7;

       USEOBSERVATIONS are  sex01 EQ 0;

Define: p1=fev1_1; p2=fev1_2; p3=fev1_3; p5=fev1_5; p6=fev1_6; p7=fev1_7;
        c1=idigf_r1; c2=idigf_r2; c3=idigf_r3; c5=idigf_r5; c6=idigf_r6; c7=idigf_r7;

        Bage=intakeage-70;   !Centered at Age 70
        !Bagesq=(intakeage-70)**2; 
         
        Sex=0;         !0=Male 
        cEduc=educ-1;     ! center at 1 as most equivalent to 7 years
        !SEP=SESgrp; 
        !Marstat; 
        Height=htcm-172;  !center at 172 for males and 160 for females
        !Weight
        !BMI 
        !SmokHist=Smoke; 
        !Alcohol; 
        !CompExp; 
        !PhysAct; 
        !BP;
        !DepSymp;
        !Cardio;
        !Diab; 
        !MorbInd;

        !BageSex=Bage*Sex;
        !BageEduc=Bage*Educ;
        !SexEduc=Sex*Educ;

ANALYSIS: TYPE = RANDOM;
	estimator=mlf;
 
MODEL:  ip sp | p1 p2 p3 p5 p6 p7 at time1 time2 time3 time5 time6 time7;
        ic sc | c1 c2 c3 c5 c6 c7 at time1 time2 time3 time5 time6 time7;
        
        ip sp ic sc ON Bage ceduc height diabete eversmoke chd;

        p2-p7 (res_p);
        c2-c7 (res_c);
        
        p2 p3 p5 p6 p7 pwith c2 c3 c5 c6 c7(res_cov);
        
        ip (v_ip); ! v - variance
        sp (v_sp); 
        ic (v_ic);
        sc (v_sc);
        ip WITH sp (c_ipsp); ! c - covariance
        ip WITH ic (c_ipic);
        ip WITH sc (c_ipsc);
        sp WITH ic (c_spic);
        sp WITH sc (c_spsc);
        ic WITH sc (c_icsc);

MODEL CONSTRAINT:

        NEW r_ipic;
        NEW r_spsc;
        NEW r_res_pc;

        r_ipic = c_ipic/((v_ip**0.5)*(v_ic**0.5));
        r_spsc = c_spsc/((v_sp**0.5)*(v_sc**0.5));
        r_res_pc = res_cov/((res_p**0.5)*(res_c**0.5));

OUTPUT: sampstat Cinterval;  

PLOT: Type is Plot1 Plot2 Plot3; 

SAVEDATA:
 
        FILE IS   SATSASlopes_m_fev_dsf;  
        SAVE = FSCORES;



