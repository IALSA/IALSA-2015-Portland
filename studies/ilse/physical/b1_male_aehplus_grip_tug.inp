TITLE:  Handgrip, Timed Up and Go, AEHplus, Male

DATA:  File = C:\Users\Besitzer\Desktop\ilse 19.10.dat;

VARIABLE: Names are 
T1SD18 ID SEX t1dss t1bd t1fs t1fsb t1age center 
t1beer t1wine t1k t1pc t1vf 
    t1sa t1spf t2dss t2bd t2fs t2fsb t2beer t2wine 
    t2k t2pc t2vf t2sa t2spf t3dss t3bd t3k t3pc t3vf 
    t3sa t3spf t2age t3age T1TUG t1height t1weight 
    T2TUG t2height t2weight T3TUG t3height t3weight 
    time1 time2r time3r time3 time2 T1HAVIR1 T1HAVIL1 
    T1HAVIR2 T1HAVIL2 T2HAVIR1 T2HAVIR2 T2HAVIL1 
    T2HAVIL2 T3HAVIR1x T3HAVIR2x T3HAVIL1x T3HAVIL2x 
    t1hg t2hg t3hg T1APEC T1INFAR T1RAUCH Educ element 
    Haupt second others Smoker Educnew heartdis;


       MISSING are ALL(-9999);
       USEVAR are time1 time2 time3 Educnew heartdis Smoker
                   p1 p2 p3 c1 c2 c3
                   Bage height;
       TSCORES = time1 time2 time3;


       USEOBSERVATIONS are sex EQ 0;

Define: p1=t1hg; p2=t2hg; p3=t3hg;
        c1=t1tug; c2=t2tug; c3=t3tug; 

        Bage=t1age-70;   !Centered at Age 70
        Bagesq=(t1age-70)**2;
        height=t1height-172; !centered for male

ANALYSIS: TYPE = RANDOM; Estimator=MLF;
 
MODEL:  ip sp | p1-p3 AT time1-time3;
        ic sc | c1-c3 AT time1-time3;
    
     ip (v_ip);
          sp (v_sp);
          ic (v_ic);
          sc (v_sc);
          ip WITH sp (c_ipsp);
          ip WITH ic (c_ipic);
          ip WITH sc (c_ipsc);
          sp WITH ic (c_spic);
          sp WITH sc (c_spsc);
          ic WITH sc (c_icsc);



        ip sp ic sc ON Educnew heartdis Smoker Bage height;

        p1-p3 (res_p);
        c1-c3 (res_c);
        p1-p3 pwith c1-c3(res_cov); !Paired covariances constrained to be equal

  MODEL CONSTRAINT:

          NEW r_ipic; !assigns labels to new parameters;
          NEW r_spsc;
          NEW r_res_pc;

          r_ipic = c_ipic/((v_ip**0.5)*(v_ic**0.5));
          r_spsc = c_spsc/((v_sp**0.5)*(v_sc**0.5));
          r_res_pc = res_cov/((res_p**0.5)*(res_c**0.5));

  OUTPUT: sampstat Cinterval;