TITLE: M6 Pulmonary Function, Block Design, Fully Conditional, Male

DATA:  File = octomult_12aug2010.dat;

VARIABLE: Names are 
   Case PairID TwinID Zygosity Female Educyrs Educcat SESgrp Resgrp Smoke
   SESChild SESChOth  HlthLif CogPrev LonelyE
   Dead DeadAge YTDead TotDem DemEver DemType DemAge YTDem
   time1 time2 time3 time4 time5 
   dedtime1 dedtime2 dedtime3 dedtime4 dedtime5 tvdead1 tvdead2 tvdead3 tvdead4 tvdead5 
   demtime1 demtime2 demtime3 demtime4 demtime5 tvdem1 tvdem2 tvdem3 tvdem4 tvdem5 
   CompAge1-CompAge5 Marital1-Marital5 weight1-weight5 height1-height5 bmi1-bmi5
   SRhlth1-SRhlth5 living1-living5 
   SR2hlth1-SR2hlth5 SROhlth1-SROhlth5 HlthPrv1-HlthPrv5 HlthTra1-HlthTra5
   CogPres1-CogPres5 AGames1-AGames5 ACross1-ACross5 
   ALiter1-ALiter5 AWrite1-AWrite5 AStudy1-AStudy5 AOther1-AOther5
   LonelyP1-LonelyP5 FrTalk1-FrTalk5 FrPart1-FrPart5
   LackCo1-LackCo5 Abandon1-Abandon5 NumFr1-NumFr5 NumPref1-NumPref5 SeeChGr1-SeeChGr5
   pek1-pek5 gripp1-gripp5 chr5tim1-chr5tim5 padl1-padl5 iadl1-iadl5 
   digspf1-digspf5 digspb1-digspb5 prose1-prose5 block1-block5 info1-info5 
   synnum1-synnum5 figure1-figure5 digsym1-digsym5 psif1-psif5
   mirnam1-mirnam5 mirrcl1-mirrcl5 mirrcg1-mirrcg5 mircor1-mircor5 clock1-clock5
   mmse1-mmse5 mismmse1-mismmse5 fmmse1-fmmse5
   highBP1-highBP5 sbp1-sbp5 dbp1-dbp5 diab1-diab5;

MISSING are .; !specification of missing observations coded in data file
USEVAR are  time1 time2 time3 time4 time5
		   p1 p2 p3 p4 p5
		   c1 c2 c3 c4 c5
		   Bage Educ Heightc SmokHist Diabetes;
		   
TSCORES =   time1 time2 time3 time4 time5;
CLUSTER = PairID;      !(when cluster: see subpopulation and Analysis)
SUBPOPULATION are DemEver EQ 0 and female EQ 1; ! (use USEOBSERVATIONS when no ..
!USEOBSERVATIONS are DemEver EQ 0 and female EQ 0;

Define: p1=gripp1; p2=gripp2; p3=gripp3; p4=gripp4; p5=gripp5;
        c1=pek1; c2=pek2; c3=pek3; c4=pek4; c5=pek5;

              Bage=compage1-80;   !Centered at Age 80
              Sex=female;         !0=Male
              Educ=Educyrs-7;     ! Centered at 7 years
              Heightc=Height1-160; !(Centered as: women = 160, men = 172)
              SmokHist=Smoke;
              Diabetes=diab1;

      ANALYSIS: TYPE = RANDOM COMPLEX; !Type of analysis requested,
 
MODEL:  ip sp | p1-p5 AT time1-time5;
        ic sc | c1-c5 AT time1-time5;
        
		ip (v_ip); 
        sp (v_sp); 
        ic (v_ic); 
        sc (v_sc);
        ip WITH sp (c_ipsp);
        ip WITH ic (c_ipic);
        ip WITH sc (c_ipsc);
        sp WITH ic (c_spic);
        sp WITH sc (c_spsc);
        ic WITH sc (c_icsc);

         ip sp ic sc ON Bage Educ Heightc SmokHist Diabetes;
        p1-p5 (res_p);
        c1-c5 (res_c);
        p1-p5 pwith c1-c5(res_cov); !Paired covariances constrained to be equal
	
		
MODEL CONSTRAINT:

        NEW r_ipic;
        NEW r_spsc;
        NEW r_res_pc;
         
        r_ipic = c_ipic/((v_ip**0.5)*(v_ic**0.5)); 
        r_spsc = c_spsc/((v_sp**0.5)*(v_sc**0.5)); 
        r_res_pc = res_cov/((res_p**0.5)*(res_c**0.5)); 

OUTPUT: sampstat Cinterval;  


